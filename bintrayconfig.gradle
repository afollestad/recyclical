if (!project.rootProject.file('local.properties').exists()) {
  println "Not applying bintrayconfig.gradle"
  return
}
apply plugin: 'com.novoda.bintray-release'

def getBintrayUserAndKey() {
  Properties properties = new Properties()
  properties.load(project.rootProject.file('local.properties').newDataInputStream())
  return [
      properties.getProperty("bintray.user"),
      properties.getProperty("bintray.apikey")
  ]
}

if (artifactName == null) {
  throw new IllegalStateException("Must specify a artifactName in module's build.gradle")
} else if (versions == null || versions.publishVersion == null) {
  throw new IllegalStateException("Unable to reference publishVersion")
}

task checkBintrayConfig {
  doLast {
    def (user, key) = getBintrayUserAndKey()
    if (user == null || user.isEmpty() ||
        key == null || key.isEmpty()) {
      throw new IllegalStateException("Must specify Bintray user/API key in your local.properties.")
    }
  }
}

afterEvaluate {
  bintrayUpload.dependsOn checkBintrayConfig
}

def (user, key) = getBintrayUserAndKey()
publish {
  bintrayUser = user
  bintrayKey = key
  userOrg = 'drummer-aidan'
  groupId = 'com.afollestad'
  artifactId = artifactName
  publishVersion = versions.publishVersion
  desc =
      'A Kotlin DSL API to make RecyclerViews easier to use and faster to setup.'
  website = 'https://github.com/afollestad/recyclical'
  dryRun = useDryRun
}